#version 450 core

#define TERRAIN_SET 0
#define TERRAIN_BINDING 0
#include "terrain_buffer.include"
#include "terrain.include"

#define WORK_GROUP_SIZE 1024
layout(local_size_x = WORK_GROUP_SIZE, local_size_y = 1, local_size_z = 1) in;

layout(push_constant) uniform frame_data_t
{
	mat4 vp;
	vec4 camera_position;
	vec2 screen_size;
	float threshold;
	float area_multiplier;
	float curvature_multiplier;
	uint node_index;
} frame_data;




// Returns true if p is valid NDC
bool clip(in vec4 p)
{
	return (abs(p.x) <= p.w &&
			abs(p.y) <= p.w &&
			abs(p.z) <= p.w);
}

float curvature(in vec3 p)
{
	const float pi = 3.1415;

	float camera_distance = distance(p, frame_data.camera_position.xyz);

	float sample_step = 5.5 + pow(camera_distance * 0.5, 0.6);
	const float gaussian_width = 1.0;
	const int filter_radius = 2;	// Side length of grid is filter_radius * 2 + 1
	const int filter_side = filter_radius * 2 + 1;
	float log_filter[filter_side * filter_side];

	/////////////////////////////////////////
	float sum = 0.0;

	for (int x = -filter_radius; x <= filter_radius; x++)
	{
		for (int y = -filter_radius; y <= filter_radius; y++)
		{
			// https://homepages.inf.ed.ac.uk/rbf/HIPR2/log.htm
			float t = -((x * x + y * y) / (2.0 * gaussian_width * gaussian_width));
			float log = -(1 / (pi * pow(gaussian_width, 4.0))) * (1.0 + t) * exp(t);

			log_filter[(y + filter_radius) * filter_side + (x + filter_radius)] = log;
			sum += log;
		}
	}

	// Normalize filter
	float correction = 1.0 / sum;
	for (uint i = 0; i < filter_side * filter_side; i++)
	{
		log_filter[i] *= correction;
	}

	float curvature = 0.0;

	for (int x = -filter_radius; x <= filter_radius; x++)
	{
		for (int y = -filter_radius; y <= filter_radius; y++)
		{
			curvature += terrain(p.xz + vec2(sample_step * x, sample_step * y)) * log_filter[(y + filter_radius) * filter_side + (x + filter_radius)];
		}
	}

	// Normalize for height
	curvature -= terrain(p.xz);

	return abs(curvature);
}

const uint max_new_normal_points = TRIANGULATE_MAX_NEW_NORMAL_POINTS / WORK_GROUP_SIZE;
vec4 new_points[max_new_normal_points + 1];
shared uint s_counts[WORK_GROUP_SIZE];
shared uint s_total;

shared float s_border_max[4];

void main(void)
{
	const uint thid = gl_GlobalInvocationID.x;
	const uint node_index = frame_data.node_index;

	const vec2 node_min = terrain_buffer.data[node_index].min;
	const vec2 node_max = terrain_buffer.data[node_index].max;
	const float side = node_max.x - node_min.x;

	const int neighbur_indexing_x[4] = { 0, 1, 0, -1 };
	const int neighbur_indexing_y[4] = { 1, 0, -1, 0 };

	const int cx = int((node_min.x - terrain_buffer.quadtree_min.x + 1) / side);  // current node x
	const int cy = int((node_min.y - terrain_buffer.quadtree_min.y + 1) / side);  // current node z/y

	const int nodes_per_side = 1 << quadtree_levels;

	if (thid == 0)  // TODO: Optimize to multiple threads
	{
		for (uint bb = 0; bb < 4; ++bb)  // TODO: Go through corner neighbour nodes as well
		{
			int nx = cx + neighbur_indexing_x[bb];
			int ny = cy + neighbur_indexing_y[bb];

			bool valid_neighbour = ny >= 0 && ny < nodes_per_side && nx >= 0 && nx < nodes_per_side;
			uint neighbour_index = 0;
			if (valid_neighbour)
			{
				neighbour_index = terrain_buffer.quadtree_index_map[ny * nodes_per_side + nx];
				uint neighbour_border = (bb + 2) % 4;

				s_border_max[bb] = terrain_buffer.data[neighbour_index].border_max[neighbour_border];
			}
		}
	}

	barrier();
	memoryBarrierShared();

	const uint index_count = terrain_buffer.data[node_index].index_count;

	uint new_point_count = 0;

	// For every triangle
	for (uint i = thid * 3; i + 3 <= index_count && new_point_count < max_new_normal_points; i += WORK_GROUP_SIZE * 3)
	{
		// Get vertices
		vec4 v0 = terrain_buffer.data[node_index].positions[terrain_buffer.data[node_index].indices[i    ]];
		vec4 v1 = terrain_buffer.data[node_index].positions[terrain_buffer.data[node_index].indices[i + 1]];
		vec4 v2 = terrain_buffer.data[node_index].positions[terrain_buffer.data[node_index].indices[i + 2]];

		// Get clipspace coordinates
		vec4 c0 = frame_data.vp * v0;
		vec4 c1 = frame_data.vp * v1;
		vec4 c2 = frame_data.vp * v2;

		// Check if any vertex is visible (shitty clipping)
		if (clip(c0) || clip(c1) || clip(c2))
		{
			// Calculate screen space area

			c0 /= c0.w;
			c1 /= c1.w;
			c2 /= c2.w;

			// a, b, c is triangle side lengths
			float a = distance(c0.xy, c1.xy);
			float b = distance(c0.xy, c2.xy);
			float c = distance(c1.xy, c2.xy);

			// s is semiperimeter
			float s = (a + b + c) * 0.5;

			float area = pow(s * (s - a) * (s - b) * (s - c), frame_data.area_multiplier);

			vec3 mid = (v0.xyz + v1.xyz + v2.xyz) / 3.0;
			float curv = pow(curvature(mid), frame_data.curvature_multiplier);

			// A new point should be added
			if (curv * area >= frame_data.threshold)
			{
				// Check if the point could be a border point
				const vec4 point = vec4(mid.x, -terrain(mid.xz) - 0.5, mid.z, 1.0);
				// TODO: Check against corner neighbour nodes as well
				bool border = false;
				// Left
				uint count = terrain_buffer.data[node_index].new_border_point_count[3];
				if (point.x < node_min.x + s_border_max[3] && count < max_border_triangle_count)
				{
					terrain_buffer.data[node_index].new_border_points[3 * max_new_border_points + count] = point;
					terrain_buffer.data[node_index].new_border_point_count[3]++;
					border = true;
				}
				// Right
				count = terrain_buffer.data[node_index].new_border_point_count[1];
				if (point.x > node_max.x - s_border_max[1] && count < max_border_triangle_count)
				{
					terrain_buffer.data[node_index].new_border_points[1 * max_new_border_points + count] = point;
					terrain_buffer.data[node_index].new_border_point_count[1]++;
					border = true;
				}
				// Top
				count = terrain_buffer.data[node_index].new_border_point_count[0];
				if (point.z > node_max.y - s_border_max[0] && count < max_border_triangle_count)
				{
					terrain_buffer.data[node_index].new_border_points[0 * max_new_border_points + count] = point;
					terrain_buffer.data[node_index].new_border_point_count[0]++;
					border = true;
				}
				// Bottom
				count = terrain_buffer.data[node_index].new_border_point_count[2];
				if (point.z < node_min.y + s_border_max[2] && count < max_border_triangle_count)
				{
					terrain_buffer.data[node_index].new_border_points[2 * max_new_border_points + count] = point;
					terrain_buffer.data[node_index].new_border_point_count[2]++;
					border = true;
				}

				// Not a border point
				if (!border)
				{
					new_points[new_point_count] = point;
					++new_point_count;
				}
			}
		}
	}




	////// PREFIX SUM

	const uint n = WORK_GROUP_SIZE;

	// Load into shared memory
	s_counts[thid] = new_point_count;

	barrier();
	memoryBarrierShared();

	if (thid == 0)
		s_total = s_counts[n - 1];

	barrier();
	memoryBarrierShared();

	int offset = 1;
	for (uint d = n >> 1; d > 0; d >>= 1) // Build sum in place up the tree
	{
		barrier();
		memoryBarrierShared();
		if (thid < d)
		{
			uint ai = offset * (2 * thid + 1) - 1;
			uint bi = offset * (2 * thid + 2) - 1;
			s_counts[bi] += s_counts[ai];
		}
		offset *= 2;
	}
	if (thid == 0) { s_counts[n - 1] = 0; } // Clear the last element
	for (int d = 1; d < n; d *= 2) // Traverse down tree & build scan
	{
		offset >>= 1;
		barrier();
		memoryBarrierShared();
		if (thid < d)
		{
			uint ai = offset * (2 * thid + 1) - 1;
			uint bi = offset * (2 * thid + 2) - 1;

			uint t = s_counts[ai];
			s_counts[ai] = s_counts[bi];
			s_counts[bi] += t;
		}
	}
	barrier();
	memoryBarrierShared();

	uint prev_count = terrain_buffer.data[node_index].new_points_count;


	barrier();
	memoryBarrierShared();
	memoryBarrierBuffer();

	// Make sure the total is saved as well
	if (thid == 0)
	{
		s_total += s_counts[n - 1];
		terrain_buffer.data[node_index].new_points_count += s_total;
		terrain_buffer.data[node_index].new_points_count = min(terrain_buffer.data[node_index].new_points_count, num_new_points);
	}
	
	// Write points to output storage buffer
	const uint base_offset = prev_count + s_counts[thid];
	for (uint i = 0; i < new_point_count && base_offset + i < num_new_points; ++i)
	{
		terrain_buffer.data[node_index].new_points[base_offset + i] = new_points[i];
	}
}